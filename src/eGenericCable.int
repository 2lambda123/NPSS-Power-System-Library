// ****************************************************************************
// * NASA Glenn Research Center
// * 21000 Brookpark Rd
// * Cleveland, OH 44135
// * Jeffrey Csank, David Sadey, Tom Lavelle
// * March 30, 2018
// ****************************************************************************

//#include <InterpIncludes.ncp>
class eGenericCable extends Element {
	
	//------------------------------------------------------------
	//     ******* DOCUMENTATION *******
	//------------------------------------------------------------
	
	title = "";
	
	description = isA() + " Determines power for the cable";
	
	usageNotes = isA() + "- NOTE TO USERS: This file....";
	
	background = "";
	
	//------------------------------------------------------------
	//     ******* SETUP VARIABLES ********
	//------------------------------------------------------------
	
	real SpecificAmperage {
		value = 170.; IOstatus="input"; //units=;
		description = "Amps to Weight/length ratio for the cable";
	}
	
	real CableLength {
		value = 28.3; IOstatus="input"; //units=;
		description = "One way line length (m)";
	}
	
	real LineResistance {
		value = 0.0000014; IOstatus="input"; //units=Amps;
		description = "Cable Resistance";
	}
	
	real Mass {
		value=0; IOstatus="output"; //units="kg";
		description = "Mass output of the cable";
	}
	
	real Loss {
		value = 0; IOstatus="output"; units="W";
		description = "Cable Loss";
	}
	
	real Vdrop {
		value = 0; IOstatus="output"; units="W";
		description = "Voltage Drop across the line";
	}
	
	//------------------------------------------------------------
	// ****** SETUP PORTS, FLOW STATIONS, SOCKETS, TABLES ********
	//------------------------------------------------------------	
	ElectricPort EP_O {
		description = "Electric port";
	}
	
	ElectricPort EP_I {
		description = "Electric Port";
	}
	
	//------------------------------------------------------------
	//   ******* PERFORM ENGINEERING CALCULATIONS *******
	//------------------------------------------------------------
	Option switchDes {
		allowedValues = { "DESIGN", "OFFDESIGN" } ;
		description = "Determines if the element is in design or off-design mode";
		rewritableValues = FALSE;
		trigger=FALSE;
	}
	
	void calculate() {

		// Supply side of the component
		EP_I.copyport();

		// Determine the load voltage using the voltage drop
		real Vdrop = EP_I.Current * LineResistance;		
		real Vload = EP_I.Voltage - Vdrop;
		
		//Determine power on load side using voltage drop across cable
		EP_O.setIV(EP_I.Current , Vload);
		Loss = 3 * EP_I.Current * (EP_I.Voltage - Vload) / 1000.;
		//Loss = EP_I.RealPower - EP_O.RealPower;
		
		//Detemine load power using I^2 losses
		//Loss = 3 * EP_I.Current**2 * LineResistance / 1000.;
		//EP_O.setIP(EP_I.Current, EP_O.RealPower);

		if (switchDes=="DESIGN"){
			/***************************
			On-Design
			in on design, calculate the mass of the cable
			***************************/
			Mass=EP_I.Current/SpecificAmperage*CableLength*3;
		}
	}
}
